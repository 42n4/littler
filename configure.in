dnl Process this file with autoconf to produce a configure script.

dnl Every other copy of the package version number gets its value from here
AC_INIT(littler,0.0.5)

dnl  Look for ancillary configure tools in tools
AC_CONFIG_AUX_DIR(tools)

dnl Options for Automake
dnl
dnl no-texinfo.tex - obviously means don't include the big texinfo.tex file
dnl                  in the dist.
dnl foreign - treats our package as non-gnu, essentially excudluding strict
dnl           checks for certain files like ChangeLog, NEWS, README, etc
dnl
AM_INIT_AUTOMAKE( no-texinfo.tex foreign )

dnl create a config.h file (Automake will add -DHAVE_CONFIG_H)
AC_CONFIG_HEADERS(config.h)

AC_SUBST(VERSION)

AC_CANONICAL_HOST

dnl Checks for programs.
AC_PROG_INSTALL
AC_PROG_CC

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS( stdio.h string.h errno.h stdlib.h sys/types.h sys/stat.h unistd.h)
AC_CHECK_HEADER([getopt.h],
	[AC_DEFINE([HAVE_GETOPT_H], 1,
		 [Define to 1 if you have <getopt.h>.])],
	[AC_MSG_ERROR([littler needs getopt support to run. Please find and install it.])])

AC_HEADER_MAJOR

AC_CHECK_FUNC(setenv,AC_DEFINE(HAVE_SETENV,1, [Define if you have the setenv function.]))

dnl Checks for typedefs, structures, and compiler characteristics.

dnl Checks for library functions.

dnl Let configure find R using the AC_PATH_PROG macro
AC_PATH_PROG(RPROG,R)

dnl Test the R version for shared library
AC_DEFUN([R_PROG],[
    AC_MSG_CHECKING([if R was built as a shared library])

	# First test if R was found
	if test "${RPROG}" = ""; then
		echo
		echo "R not found. Please update your PATH variable to include the R bin directory"
		echo
		AC_MSG_ERROR( aborting! )
	fi

    # Now make sure that a well known include file exists
    # and the libR.so
    if ${RPROG} CMD config --ldflags 2>/dev/null | grep -v 'R was not built as a shared library' >/dev/null; then
        RHOME=`$RPROG RHOME`
		AM_CFLAGS=`${RPROG} CMD config --cppflags`
		if test "$build_vendor" != "apple"; then
			AM_LDFLAGS="`${RPROG} CMD config --ldflags` -Wl,-rpath,${RHOME}/lib"
			RUN_NAMETOOL=""
		else
			AM_LDFLAGS="`${RPROG} CMD config --ldflags`"
		fi
        AC_MSG_RESULT([yes])
    else
        echo
        echo "R was not built as a shared library"
        echo
        echo "Either build it with one, or use another install of R"
        echo
        AC_MSG_ERROR( aborting! )
    fi
])

R_PROG

dnl Are we building for Mac OS X? this program will exist if so.
AC_PATH_PROG(NAMETOOL,install_name_tool)

AC_DEFUN([NAMETOOL_FUN],[
	if test "${NAMETOOL}" != ""; then
			RUN_NAMETOOL="${NAMETOOL} -change libR.dylib ${RHOME}/lib/libR.dylib r"
	fi
])

NAMETOOL_FUN

AC_SUBST(RPROG)
AC_SUBST(RHOME)
AC_SUBST(RUN_NAMETOOL)
AC_SUBST(AM_CFLAGS)
AC_SUBST(AM_LDFLAGS)

AC_OUTPUT(Makefile littler.h)
